apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    namespace 'de.efdis.tangenerator'

    compileSdkVersion versions.compileSdk

    defaultConfig {
        versionCode 69
        versionName "1.2.0"

        // Requires API level 23 (Android 6.0) to use Android keystore system for cryptography.
        minSdkVersion 23
        targetSdkVersion versions.targetSdk
        resourceConfigurations += ['de', 'en']


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                // Export database schema history as JSON files.
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        sourceSets {
            // Include database schema history for migration testing.
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig = true
    }

    buildTypes {
        debug {
            // Don't mess with the release versions during debugging, so use a different appId.
            applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions += ["client"]
    productFlavors {
        EFD {
            // Demo portal efdis-online.de (extern) / dailybuild.efdis-online.de (intern)
            dimension "client"
            applicationId "de.efdis.activeTAN"
        }

        VAR {
            dimension "client"
            applicationId "de.varengold.activeTAN"
        }

    }

    compileOptions {
        // ZXing uses Java 8 language features from the core library
        coreLibraryDesugaringEnabled true

        targetCompatibility JavaVersion.VERSION_11
        sourceCompatibility JavaVersion.VERSION_11
    }

    testOptions {
        // Disable animations for espresso tests to speed up execution.
        animationsDisabled = true
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

tasks.withType(JavaCompile).configureEach {
    options.deprecation = true
}

tasks.withType(Test).configureEach {
    maxParallelForks = 1
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':material-design-icons')
    implementation project(":barcodescanner")

    implementation 'com.google.android.material:material:1.13.0'

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.biometric:biometric:1.1.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.core:core-ktx:1.17.0'

    def activity_version = '1.11.0'
    // Java language implementation
    implementation "androidx.activity:activity:$activity_version"
    // Kotlin
    implementation "androidx.activity:activity-ktx:$activity_version"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    implementation 'com.google.zxing:core:' + versions.zxing

    def room_version = '2.8.0'
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.4'
    testImplementation "androidx.test:core:1.7.0"
    testImplementation 'androidx.test:monitor:1.8.0'
    testImplementation "androidx.test.services:test-services:1.6.0"
    testImplementation "androidx.test:orchestrator:1.6.1"
    testImplementation "androidx.test.uiautomator:uiautomator:2.3.0"
    testImplementation "androidx.benchmark:benchmark-macro-junit4:1.4.1"
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test:rules:1.7.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0'

    def espresso_version = '3.7.0'
    implementation "androidx.test.espresso:espresso-idling-resource:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"

    testImplementation "androidx.fragment:fragment-testing-manifest:1.8.9"
}
